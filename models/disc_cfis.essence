language ESSENCE' 1.0

given db : mset of record { itemset : set of int, class : int }

given min_freq : int

letting db_minValue be  min([val | entry <- db, val <- entry[itemset]])
letting db_maxValue be max([val | entry <- db, val <- entry[itemset]])
letting db_maxEntrySize be max([ |entry[itemset]| | entry <- db ])
letting db_row_size be |db|
$ letting db_min_class_value be min([entry[class] | entry <- db])
$ letting db_max_class_value be max([entry[class] | entry <- db])

given utility_values : matrix indexed by [int(db_minValue..db_maxValue)] of int
given cost_values : matrix indexed by [int(db_minValue..db_maxValue)] of int

given min_utility : int
given max_cost : int

letting support_domain be domain int(1..db_row_size)

find freq_items : record { 
                        itemset : set (minSize 1, maxSize db_maxEntrySize) of int(db_minValue..db_maxValue), 
                        support_pos : support_domain,
                        support_neg : support_domain
                    } 

$ frequency
such that
    (sum entry in db . toInt(freq_items[itemset] subsetEq entry[itemset] /\ entry[class] = 1)) = freq_items[support_pos]

such that
    (sum entry in db . toInt(freq_items[itemset] subsetEq entry[itemset] /\ entry[class] = 0)) = freq_items[support_neg]

such that
    $ [freq_items[support_func](1) >= min_freq /\ freq_items[support_func](0) < min_freq, freq_items[support_func](0) >= min_freq /\ freq_items[support_func](1) < min_freq; int(0..1) ][toInt(freq_items[wanted_class]=0)]
    
    $ Directly pos - negative > threshold or neg - pos > threshold instead. wanted class is the predominant one. Do other experiments later.

    $ we can only look pos - neg easily instead
     
    freq_items[support_pos] - freq_items[support_neg] > min_freq
    
    

$ utilities

such that
    (sum item in freq_items[itemset] . utility_values[item]) >= min_utility

such that
    (sum item in freq_items[itemset] . cost_values[item]) <= max_cost


dominance_relation (freq_items[itemset] subsetEq fromSolution(freq_items[itemset])) -> (freq_items[support_pos] > fromSolution(freq_items[support_pos]))

incomparability_function descending |freq_items[itemset]|

    
